buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 23
        compileSdkVersion = 34
        targetSdkVersion = 34
        ndkVersion = "25.1.8937393"
        kotlinVersion = "2.0.21"
        reactNativeVersion = "0.73.2"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://www.jitpack.io" }
    }
    
    // Handle dependencies from older libraries
    configurations.all {
        resolutionStrategy {
            // Force our react-native version
            force "com.facebook.react:react-android:${rootProject.ext.reactNativeVersion}"
            force "com.facebook.react:hermes-android:${rootProject.ext.reactNativeVersion}"
            
            // Redirect old react-native artifact name to new react-android
            eachDependency { details ->
                if (details.requested.group == 'com.facebook.react' && details.requested.name == 'react-native') {
                    details.useTarget("com.facebook.react:react-android:${rootProject.ext.reactNativeVersion}")
                    details.because("react-native renamed to react-android in 0.71+")
                }
            }
        }
    }
}
